#!/usr/bin/env zsh
# trash - Move files to the trash directory instead of deleting them.
# Usage: trash <file>...

help() {
    cat <<EOF

trash - move files to the trash directory instead of deleting them

Usage: trash <file>...
EOF
}

if [ -z "$1" ] || [[ "$1" == "-h" ]] || [[ "$1" == "--help" ]]; then
    help
    return 0
fi

prompt() {
    echo "$1 [y/N] \c"
    read -r confirmation
    if [[ $(echo "$confirmation" | tr "[:upper:]" "[:lower:]") != "y" ]]; then
        echo "Exiting."
        return 1
    fi
}

TRASH_DIR="${XDG_TRASH_HOME:-${HOME}/.Trash}"
if [ ! -d "$TRASH_DIR" ]; then
    echo "Could not find $TRASH_DIR directory..."
    prompt "Create it?" || return 1
    mkdir "$TRASH_DIR"
fi

trash_deletion_limit_mb=500
failed=()
successful=0
total_deleted_mb=0

for var in "$@"; do
    if [ ! -e "$var" ]; then
        failed+=("$var")
        continue
    fi
    filename=$(basename "$var")
    destination="$TRASH_DIR/$filename"
    if [ -e "$destination" ]; then
        dt=$(strftime '%-I.%M.%S %p' $(date +%s))
        if [ -d "$var" ]; then
            destination="$TRASH_DIR/${filename} ${dt}"
        else
            destination="$TRASH_DIR/${filename%.*} ${dt}.${filename##*.}"
        fi
    fi
    size="$(du -sm "$var" | cut -f1)"
    if [ "$size" -gt "$trash_deletion_limit_mb" ]; then
        echo "File '$var' ($(numfmt --from=si --to=si ${size}M)B) is larger than $(numfmt --from=si --to=si ${trash_deletion_limit_mb}M)B."
        prompt "Are you sure you want to move it to trash?" || return 1
    fi
    if mv "$var" "$destination"; then
        ((successful=successful+1))
        ((total_deleted_mb=total_deleted_mb+size))
        touch "$destination"
    else
        failed+=("$var")
    fi
done
if [ $successful -gt 0 ]; then
    echo "Trashed ${successful} file$([[ $successful -eq 1 ]] || echo "s") ($(numfmt --from=si --to=si ${total_deleted_mb}M)B)."
fi
if [ ${#failed[@]} -gt 0 ]; then
    echo "Failed (${#failed[@]}):"
    for file in "${failed[@]}"; do
        echo "  $file"
    done
fi